def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext {
    // Support library version
    supportLibraryVersion = '28.0.0'

    // AppCompat
    depsAppCompat = dependencyGroup {
        implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    }

    // Design
    depsDesign = dependencyGroup {
        implementation "com.android.support:design:$supportLibraryVersion"
        implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
        implementation "com.android.support:cardview-v7:$supportLibraryVersion"
        // ConstraintLayout
        implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    }

    // Dagger
    depsDagger = dependencyGroup {
        implementation "com.google.dagger:dagger:2.16"
        kapt "com.google.dagger:dagger-compiler:2.16"
        kapt "com.google.dagger:dagger-android-processor:2.16"
    }

    // Kotlin
    depsKotlin = dependencyGroup {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }

    // RxKotlin
    depsRxKotlin = dependencyGroup {
        implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
        implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    }

    // MapStruct
    depsMapStruct = dependencyGroup {
        implementation 'org.glassfish:javax.annotation:3.1.1'
        implementation 'org.mapstruct:mapstruct:1.2.0.Final'
        kapt 'org.mapstruct:mapstruct-processor:1.2.0.Final'
    }

    // Logger
    depsLogger = dependencyGroup {
        implementation 'ru.digipeople.logger:logger-kotlin:1.0.0'
    }

    // Retrofit
    depsRetrofit = dependencyGroup {
        implementation 'com.google.code.gson:gson:2.8.2'
        implementation 'com.squareup.retrofit2:retrofit:2.4.0'
        implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
        implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
        implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    }

    // Tests
    depsTests = dependencyGroup {
        // JUnit
        testImplementation "junit:junit:4.12"
        // Test runner
        androidTestImplementation "com.android.support.test:runner:1.0.1"
        // Espresso
        androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
    }
}